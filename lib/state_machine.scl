FUNCTION_BLOCK "stateMachineTimer"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      state : Int;   // state machine
   END_VAR

   VAR_OUTPUT 
      q : Bool;   // out
      rt : Time;   // Remaining time
      et : Time;
   END_VAR

   VAR 
      pt : Time;   // input time
      "timer" {OriginalPartName := 'IEC_TIMER'; LibVersion := '1.0'} : IEC_TIMER;
      old_state : Int;
   END_VAR


BEGIN
	// https://github.com/rexhip/
	//
	// State machine timer.
	// 
	// The timer is reseted if:
	// - Pt hasen't got a new value (for each scan)
	// - New state
	
	#timer.TON(IN := #pt <> T#0s
	           (* *)AND #state = #old_state,
	           PT := #pt,
	           Q => #q);
	
	#et := #timer.ET;
	#rt := #pt - #et;
	
	#pt := T#0s;
	#old_state := #state;
	
END_FUNCTION_BLOCK

FUNCTION "time2str" : String
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      in : Time;
   END_VAR

   VAR_TEMP 
      t {OriginalPartName := 'DTL'; LibVersion := '1.0'} : DTL;
   END_VAR


BEGIN
	// https://github.com/rexhip/
	// 
	// Time to string.
	
	#t += #in;
	#t.DAY -= 1;
	#time2str := ''; // (Avoiding compilation warnings)
	
	IF #t.DAY <> 0 THEN
	    #time2str := CONCAT(IN1 := RIGHT(IN := USINT_TO_STRING(#t.DAY),
	                                                         L := 1 + BOOL_TO_INT(#t.DAY > 9)),
	                        IN2 := 'd ');
	END_IF;
	IF #t.HOUR <> 0 OR LEN(#time2str) <> 0 THEN
	    #time2str := CONCAT(IN1 := #time2str,
	                        IN2 := RIGHT(IN := USINT_TO_STRING(#t.HOUR),
	                                     L := 1 + BOOL_TO_INT(#t.HOUR > 9)),
	                        IN3 := 'h ');
	END_IF;
	IF #t.MINUTE <> 0 OR LEN(#time2str) <> 0 THEN
	    #time2str := CONCAT(IN1 := #time2str,
	                        IN2 := RIGHT(IN := USINT_TO_STRING(#t.MINUTE),
	                                     L := 1 + BOOL_TO_INT(#t.MINUTE > 9)),
	                        IN3 := 'm ');
	END_IF;
	#time2str := CONCAT(IN1 := #time2str,
	                    IN2 := RIGHT(IN := USINT_TO_STRING(#t.SECOND),
	                                 L := 1 + BOOL_TO_INT(#t.SECOND > 9)),
	                    IN3 := 's');
	
END_FUNCTION

FUNCTION "sTimer" : Bool
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      pt : Time;
   END_VAR

   VAR_IN_OUT 
      t : "stateMachineTimer";
   END_VAR


BEGIN
	// sTimer
	// Make a timer, a one liner, in a state macine
	
	#t.pt := #pt;
	#sTimer := #t.q;
	
END_FUNCTION

FUNCTION_BLOCK "example_stateMachine"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR 
      state : Int;
      timer1 : "stateMachineTimer";
      timer2 : "stateMachineTimer";
      "__condition_1__" : Bool := TRUE;
      "__condition_2__" : Bool := TRUE;
      HMI_status : String[100];
   END_VAR


BEGIN
	// State machine with timer example. (https://github.com/rexhip/)
	
	CASE #state OF
	    0:  // Next state after 14 sec. Note that the timer is only one line of code.
	        #state += BOOL_TO_INT("sTimer"(pt := T#14s, t := #timer1));
	        
	        #HMI_status := CONCAT(IN1 := 'State 0, remaining time: ',
	                              IN2 := "time2str"(#timer1.rt), IN3 := '.');
	        
	    1:  // Next state after 7 sec, and '__condition_1__' is true.
	        #timer1.pt := T#7s;
	        #state += BOOL_TO_INT(#timer1.q AND #"__condition_1__");
	        
	        // Back to stae 0 if 50 sec elapses. A timer can be used once in each state,
	        // therfor timer2 used for this condition. Note the ":=" and not "+=" as above.
	        #state := SEL(G := "sTimer"(pt := T#50s, t := #timer2), IN0 := #state, IN1 := 0);
	        
	        #HMI_status := CONCAT(IN1 := 'State 1, Timer1 ', IN2 := "time2str"(#timer1.rt),
	                              IN3 := ', Timer2 ', IN4 := "time2str"(#timer2.rt));
	        
	    2:  // Back to state 0, when '__condition_2__' has been true for 8 sec continuously.
	        // The sTimer FC could have been used here also, but for the sake of the
	        // example it isn't.
	        IF #"__condition_2__" THEN
	            #timer1.pt := T#8s;
	            #state := SEL(G := #timer1.q, IN0 := #state, IN1 := 0);
	        END_IF;
	        
	        #HMI_status := 'State 2.';
	END_CASE;
	
	#timer1(state := #state); // Run the timer code.
	#timer2(state := #state);
	
END_FUNCTION_BLOCK

DATA_BLOCK "example_stateMachine_DB"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
NON_RETAIN
"example_stateMachine"

BEGIN

END_DATA_BLOCK

ORGANIZATION_BLOCK "example_stateMachine_OB"
TITLE = "Main Program Sweep (Cycle)"
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1

BEGIN
	"example_stateMachine_DB"();
	
END_ORGANIZATION_BLOCK


FUNCTION "timerGoto" : Void
{ S7_Optimized_Access := 'TRUE' }
VERSION : 0.1
   VAR_INPUT 
      pt : Time;
      goto : Int;
   END_VAR

   VAR_IN_OUT 
      state : Int;
      "timer" : "stateMachineTimer";
   END_VAR


BEGIN
	// sTimer
	// Make a timer, a one liner, in a state macine
	
	#timer.pt := #pt;
	IF #timer.q THEN
	    #state := #goto;
	END_IF;
	
END_FUNCTION

